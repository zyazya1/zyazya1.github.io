# 1.4.5.1 芯片固定程序解析（zxp版本）

## 一、通用部分

通用部分为一些常用指令的缩写形式，以便能更快捷的使用一些指令。

```RAPID
proc h(num a)
	moveabsj home{a},vmax,fine,tool0;
endproc
proc w(num a)
	waittime a;
endproc
proc aq(num a)
	movel aqd{a},vmax,fine,tool0;
endproc
proc tg(num a)
	movel offs(crobt(),0,0,a),v200,fine,tool0;
endproc
proc gj(num a,robtarget b,num c,num d,num e)
	if a<>0 aq a;
	!如果原料区与工位在机器人两侧，则移动到安全点2和3时应使用movej指令
	(if a=2 or a=3 movej aqd{a},v1000,fine,tool0;
	if a<>0 and a<>2 and a<>3 aq a;)
	if c>0 and c<5 then
		pdispon\rot\exep:=py{c},py{1},tool0;
		movel offs(b,0,0,d),v1000,fine,tool0;
		movel b,v200,fine,tool0;
		pdispoff;
	else
		movel offs(b,0,0,d),v1000,fine,tool0;
		movel b,v200,fine,tool0;
	endif
	test e
	case 1:
		reset hd;
		w 0.5;
	case 2:
		set hd;
		w 0.5;
	case 3:
		set vc1;
		w 0.5;
		while vs1<>1 do
			movel offs(crobt(),0,0,-1),v10,fine,tool0;
		endwhile
	case 4:
		reset vc1;
		w 0.5;
		tg 10;
	case 5:
		set vc2;
		w 0.5;
		tg 10;
		if vs2<>1 reset vc2;
	case 6:
		reset vc2;
		w 0.5;
		tg 10;
	case 7:
		w 0.2;
		set sh;
		reg1:=5;
		while reg1<30 do
			movel offs(crobt(),0,0,-0.5),v10,fine,tool0;
		endwhile
		reset sh;
		reset vc1;
	case 8:
		set vc1;
		w 0.5;
		tg 10;
		if vs1<>1 reset vc1;
	endtest
	if c>0 and c<5 then
		pdispon\rot\exep:=py{c},py{1},tool0;
		movel offs(b,0,0,d),v1000,fine,tool0;
		pdispoff;
	else
		movel offs(b,0,0,d),v1000,fine,tool0;
	endif
	if a<>0 aq a;
	!如果原料区与工位在机器人两侧，则移动到安全点2和3时应使用movej指令
	(if a=2 or a=3 movej aqd{a},v1000,fine,tool0;
	if a<>0 and a<>2 and a<>3 aq a;)
endproc
proc ks()
	waitdi di3,1;
endproc
proc js(num a)
	speedrefresh a;
endproc
proc lj()
	socketclose s1;
	socketcreate s1;
	w 0.2;
	socketconnect s1,"192.168.125.110",1500;
	w 0.1;
endproc
proc fxh(num a)
	fs{1}:=a;
	lj;
	socketsend s1\data:=fs;
	w 0.1;
endproc
proc sxh()
	lj;
	socketreceive s1\data:=jsh;
	w 0.1;
endproc
```

## 二、视觉检测部分

视觉检测部分为机器人与视觉系统的连接与拍照等指令。

```RAPID
func num ccd(num a)
	d:=0;
	socketclose s2;
	socketcreate s2;
	w 0.2;
	socketconnect s2,"192.168.125.6",2000;
	w 0.1;
	socketsend s2\str:=sc{a};
	movel dw{5},v1000,fine,tool0;
	socketsend s2\str:="m";
	w 0.2;
	socketreceive s2\str:=sr;
	ok:=strtoval(strpart(sr,7,2),d);
	if d=1 then
		ok:=strtoval(strpart(sr,10,2),d);
		if d=-1 d:=2;
	else
		d:=3;
	endif
	return d;
endfunc
```

## 三、芯片分拣部分

芯片分拣部分由三大部分组成，一是芯片的检测与搬运，二是芯片点位的计算，三是芯片状态的更新，四是工位检测指示灯的模式选择。

```RAPID
!第一部分芯片的检测与搬运
proc zxp(num a,num b,num c)
	z:=0;
	test a
	case 1:
		for i from 1 to 4 do
			for j from 1 to 5 do
				if lx{i,j}=b and bz{i,j}=c and bh{i}<>0 then
					z:=1;
					d1:=bk{i,j};
					r1:=i;
					r2:=j;
					goto bq1;
				endif
			endfor
		endfor
	case 2:
		for i from 1 to 4 do
			for j from 1 to k{i} do
				if i=b and yz{i,j}=c then
					z:=1;
					d1:=yl{i,j};
					r1:=i;
					r2:=j;
					goto bq1;
				endif
			endfor
		endfor
	case 3:
		for i from 1 to 4 do
			for j from 1 to k{i} do
				if i=b and fz{i,j}=c then
					z:=1;
					d1:=fl{i,j};
					r1:=i;
					r2:=j;
					goto bq1;
				endif
			endfor
		endfor
	endtest
	bq1:
endproc
proc bxp(num a)
	z:=0;	
	for i from 1 to 4 do
		for j from 1 to k{i} do
			if i=a and yz{i,j}<>0 then
				z:=1;
				d1:=yl{i,j};
				r1:=i;
				r2:=j;
				goto bq1;
			endif
		endfor
	endfor
	bq1:
endproc
proc ylk(num a,num b)
	zxp 2,lx{a,b},0;
	if z=1 then
		gj 2,d1,0,100,6;
		jh bz{a,b},yz{r1,r2};
	else
		zxp 3,lx{a,b},0;
		gj 3,d1,0,100,6;
		jh bz{a,b},fz{r1,r2};
	endif
endproc
proc mbfx(num a,num b,num c)
	zxp 2,lx{a,b},c;
	if z=1 then
		gj 2,d1,0,100,5;
		gj 1,bk{a,b},bgw{a},20,6;
		jh bz{a,b},yz{r1,r2};
	endif
endproc
proc yltc()
	for i from 1 to 4 do
		for j from 1 to k{i} do
			gj 2,yl{i,j},0,100,5;
			if vs2<>0 then
				yz{i,j}:=ccd(i);
				if d=3 then
					zxp 3,i,0;
					gj 3,d1,0,100,6;
					jh yz{i,j},fz{r1,r2};
				else
					gj 2,yl{i,j},0,100,6;
				endif
			else
				yz{i,j}:=0;
			endif
		endfor
	endfor
endproc
proc fltc()
	for i from 1 to 4 do
		for j from 1 to k{i} do
			gj 3,fl{i,j},0,100,5;
			if vs2<>0 then
				fz{i,j}:=ccd(i);
				if d=3 then
					zxp 3,i,0;
					gj 3,d1,0,100,6;
					jh fz{i,j},fz{r1,r2};
				else
					gj 3,fl{i,j},0,100,6;
				endif
			else
				fz{i,j}:=0;
			endif
		endfor
	endfor
endproc
proc btc()
	for i from 1 to 4 do
		for j from 1 to k{i} do
			gj 1,bk{i,j},bgw{i},20,5;
			if vs2<>0 then
				bz{i,j}:=ccd(lx{i,j});
				if d=3 then
					zxp 3,lx{i,j},0;
					gj 3,d1,0,100,6;
					jh bz{i,j},fz{r1,r2};
				else
					gj 1,bk{i,j},bgw{i},20,6;
				endif
			else
				bz{i,j}:=0;
			endif
		endfor
	endfor
endproc
proc zbh(num a,num b,num c,num d)
	bh{1}:=a;
	bh{2}:=b;
	bh{3}:=c;
	bh{4}:=d;
endproc
```

```RAPID
!第二部分芯片的点位计算
proc fjs(num a)
	for i from 2 to k{a}-1 do
		yl{a,i}.trans.x:=yl{a,1}.trans.x+(yl{a,k{a}}.trans.x-yl{a,1}.trans.x)/(k{a}-1)*(i-1);
		yl{a,i}.trans.y:=yl{a,1}.trans.y+(yl{a,k{a}}.trans.y-yl{a,1}.trans.y)/(k{a}-1)*(i-1);
		yl{a,i}.trans.z:=yl{a,1}.trans.z+(yl{a,k{a}}.trans.z-yl{a,1}.trans.z)/(k{a}-1)*(i-1);
		yl{a,i}.rot:=yl{a,1}.rot;
		yl{a,i}.robconf:=yl{a,1}.robconf;
	endfor
endproc
proc dwjs()
	fjs 1;
	fjs 2;
	for i from 1 to 4 do
		for j from 1 to k{i} do
			pdispon\rot\exep:=py{6},py{5},tool0;
			movel yl{i,j},v1000,fine,tool0;
			pdispoff;
			fl{i,j}:=crobt();
		endfor
	endfor
endproc
```

```
!芯片状态的更新
proc jh(inout num a,inout num b)
	p:=a;
	a:=b;
	b:=p;
endproc
proc qf(inout num a)
	if a=1 then
		a:=2;
	elseif a=2 then
		a:=1;
	endif
endproc
```

```
!工位检测指示灯的模式选择
proc deng(num a,num b,num c,num d)
	fxh 60+a;
	fxh 70+b;
	fxh 80+c;
	fxh 90+d;
endproc
```

## 四、中断部分

中断部分主要控制机器人降速和停止，注意中断子程序一般在程序开头调用，否则在调用之前不生效。

```
proc zd()
	idelete a4;	
	connect a4 with zdjs;
	isignaldi di4,1,a4;
	
	idelete a5;	
	connect a5 with zdhf;
	isignaldi di4,9,a5;
	
	idelete a6;	
	connect a6 with zdtz;
	isignaldi di5,1,a6;
endproc
trap zdjs
	js 20;
endtrap
trap zdhf
	js 100;
endtrap
trap zdtz
	stopmove;
	waitdi di5,0;
	startmove;
endtrap
```

